apply plugin: 'java'
apply plugin: 'com.timgroup.autobumper'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.timgroup.jarmangit'

ext {
    buildNumber = System.getenv("BUILD_NUMBER") ?: System.getenv("TRAVIS_BUILD_NUMBER")
    mainClassName = 'com.timgroup.blankapp.Launcher'
}

group = 'com.timgroup'
if (buildNumber) version = '1.0.' + buildNumber

repositories {
    maven {
        name "nexus"
        url "${repoUrl}/groups/public"
    }
}

dependencies {
    compile "com.timgroup:Tucker:autobump"
    compile "com.timgroup:tim-logger:autobump"
    compile "com.timgroup:tim-structured-events:autobump"
    compile "io.dropwizard.metrics:metrics-core:3.2.3"
    compile "io.dropwizard.metrics:metrics-jvm:3.2.3"
    compile "io.dropwizard.metrics:metrics-graphite:3.2.3"

    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest-core:1.3"
    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile "com.timgroup:tim-structured-events-testing:autobump"
}

task run(type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args += 'config.properties'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'TIM Group Ltd'
        )
    }
}

shadowJar {
    manifest {
        attributes(
                "X-Java-Version": "8",
                "Main-Class": mainClassName
        )
    }
}

task runShadow(type: Exec, dependsOn: shadowJar) {
    commandLine 'java', '-jar', tasks.shadowJar.archivePath, 'config.properties'
}

tasks.withType(JavaCompile) {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
    options.incremental = true
    options.deprecation = true
    options.compilerArgs << "-parameters"
}

assemble.dependsOn(shadowJar)

apply plugin: 'maven-publish'

publishing {
    repositories {
        maven {
            url "${project.repoUrl}/repositories/yd-release-candidates"
            credentials {
                username = project.repoUsername
                password = project.repoPassword
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId project.ext.has('artifactId') ? project.ext.artifactId
                    : project == rootProject ? project.name
                    : rootProject.name + "-" + project.name
            from components.java
            artifact(sourcesJar) {
                classifier "sources"
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

buildscript {
    repositories {
        maven {
            url "${repoUrl}/groups/public"
        }
    }

    dependencies {
        classpath 'com.timgroup:gradle-autobumper:1.1.+'
        classpath 'com.timgroup:gradle-jarmangit:1.1.+'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}
