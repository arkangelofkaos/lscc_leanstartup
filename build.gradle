apply plugin: 'java'
apply plugin: 'autobumper'
apply plugin: 'gradle-one-jar'
apply plugin: 'jarmangit'

ext {
    buildNumber = System.getenv("BUILD_NUMBER") ?: System.getenv("TRAVIS_BUILD_NUMBER")
    mainClassName = 'com.timgroup.blankapp.Launcher'
}

group = 'com.timgroup'
if (buildNumber) version = '1.0.' + buildNumber

repositories {
    maven {
        name "nexus"
        url "${repoUrl}/groups/public"
    }
}

dependencies {
    compile "com.timgroup:Tucker:autobump"
    compile "com.timgroup:tim-logger:autobump"
    compile "com.timgroup:tim-structured-events:autobump"
    compile "io.dropwizard.metrics:metrics-core:3.2.2"
    compile "io.dropwizard.metrics:metrics-jvm:3.2.2"
    compile "io.dropwizard.metrics:metrics-graphite:3.2.2"

    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest-core:1.3"
    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile "com.timgroup:tim-structured-events-testing:autobump"
}

task run(type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args 'config.properties'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'TIM Group Ltd'
        )
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
    options.incremental = true
    options.deprecation = true
    options.compilerArgs << "-parameters"
}

task oneJar(type: OneJar) {
    mainClass = mainClassName
    manifest {
        attributes 'One-Jar-URL-Factory': 'com.simontuffs.onejar.JarClassLoader$OneJarURLFactory'
        attributes("X-Java-Version": "8")
    }
    additionalDir = file('src/oneJar')
}

build.dependsOn(oneJar)

apply plugin: 'maven-publish'

publishing {
    repositories {
        maven {
            url "${project.repoUrl}/repositories/yd-release-candidates"
            credentials {
                username = project.repoUsername
                password = project.repoPassword
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId project.ext.has('artifactId') ? project.ext.artifactId
                    : project == rootProject ? project.name
                    : rootProject.name + "-" + project.name
            from components.java
            artifact(sourcesJar) {
                classifier "sources"
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

buildscript {
    repositories {
        maven {
            url "${repoUrl}/groups/public"
        }
    }

    dependencies {
        classpath group: "com.timgroup", name: "gradle-autobumper", version: "1.0.+"
        classpath group: "com.timgroup", name: "gradle-jarmangit", version: "1.0.+"
        classpath group: 'com.github.rholder', name: 'gradle-one-jar', version: '1.0.4'
    }
}
